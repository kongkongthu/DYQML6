# 控件系统构成与定制控件

[TOC]

---

## 系统构成

在DYQML系统中，除了几个特殊控件（DYColorSpace、DYShortcut、DYToolTip）以外，所有用于动态生成的控件的基类都是DObject，它规范了系统内动态生成的控件的基本属性和方法，如ctrlType、dyName、dyAnchors等，也包含了对信号的基本响应，如显示与隐藏、使能与否等。其他控件都继承自DObject，是它的派生类，或派生类的派生类。

下图描述了目前系统中包含了的控件的继承关系：

![控件类继承关系](2-组件系统构成与定制.assets/控件类继承关系.jpg)

从图中我们可以看到，除了DYColorSpace、DYShortcut、DYToolTip这三个控件外，其他控件都继承自DObject。DObject最重要的两个派生类是DcontrollerBase和DShowerBase，它们分别是控制类控件与数据显示类控件的基类。控制类控件是指不显示实时数据，但会对外发出信号指令的控件，例如按钮、复选框、下拉菜单等，都会在用户操作时产生用户事件，这样的用户事件会对外发送信号。显示类控件是用于显示数据的控件，他们会实时从DYQML的Hash表所构成的数据系统中获取数据并刷新界面显示。

DObject类本质上是一个QML的Rectangle，之所以选择Rectangle而非Item，主要原因包括：

1. Item组件虽然更基础更简单，且是Rectangle的父类，具备Rectangle的基本特性，但Item本身没有颜色属性，而对于绝大多数需求，控件颜色、包括border的颜色设计都是必须的，使用Item无法满足这种需要；
2. Rectangle本身也足够简单，没有太多复杂的不必要的属性和方法，并可以作为容器放置或动态生成其他控件；

目前系统中继承自DObject和DControllerBase的控件最多，你可以以这些控件的代码为例，开始定制自己的控件。

---

## 定制控件

要想充分利用DYQML的功能，利用DYQML的信号系统与事件响应，并充分利用动态定制的能力，使你的控件继承自DObject、DControllerBase和DShwerBase这三个类是非常明智的选择。否则你的控件很可能无法正常使用DYQML系统的动态生成特性和能力，无法有效利用DYQML的信号系统，使他们的能力打折扣。

另外要定制控件，就需要你对QML有相对的了解，知道并会用QML的基本控件类型。DYQML不推荐但也不排斥使用基于Control的控件设计，但为了保证系统性能与代码整洁，使用基于Item的基本控件类型，基本就可以搭建出任何你想要的控件了。

在此基础上，你可以通过以下简单的步骤完成一个控件的定制化开发并纳入到DYQML系统中来。下面以DYText为例，演示定制控件的一般步骤：

- **Step 1 - 新建QML控件文件：**根据控件功能，在/qml/controls目录下新建一个qml文件，注意文件名尽量符合"DYCtrlName.qml"的形式(如DYText.qml)以便于管理，且文件名必须以大写字母开头，否则Qt系统不认。

- **Step 2 - 选择继承关系：**选择好控件继承关系，并以父类为基础开始写代码内容，如DYText继承自DObject。设置控件id（id不是必须的，但为了设计控件，控件内的代码可能会用到这个id），id名称尽量保持与文件名一致，但要将DY小写。在QML中，id的首字母必须小写，否则Qt系统也会报错，如DYText的id为dyText，具体代码为：

  ```js
  import QtQuick 2.15
  DObject {
      id: dyText
  }
  ```

- **Step 3 - 设计控件：** 设计完成控件属性、外观与功能，下面是DYText的全部代码。这一步纯粹是利用QML与JS进行控件自身的设计，这些自定义的属性一般是需要在配置文件中配置的属性，如下面的text、fontSize等属性。

  ```js
  import QtQuick 2.15
  DObject {
      id: dyText
      color: "#00000000"
      property string text
      property int fontSize
      property color fontColor
      property bool fontBold
      property string textHAlignment
      property string textVAlignment
  
      Text {
          id: innerText
          anchors.fill: parent
          text: qsTr(dyText.text)
          color: fontColor
          font.pixelSize: fontSize
          font.bold: fontBold
          font.family: frontEnd.fontFamily
      }
  
      Component.onCompleted: {
          _initTextHAlignment();
          _initTextVAlignment();
      }
  
      function _initTextHAlignment(){
          switch(textHAlignment){
          case "left":
              innerText.horizontalAlignment = Text.AlignLeft;
              break;
          case "right":
              innerText.horizontalAlignment = Text.AlignRight;
              break;
          case "center":
              innerText.horizontalAlignment = Text.AlignHCenter;
              break;
          default:
              innerText.horizontalAlignment = Text.AlignLeft;
              break;
          }
      }
  
      function _initTextVAlignment(){
          switch(textVAlignment){
          case "top":
              innerText.verticalAlignment = Text.AlignTop;
              break;
          case "bottom":
              innerText.verticalAlignment = Text.AlignBottom;
              break;
          case "center":
              innerText.verticalAlignment = Text.AlignVCenter;
              break;
          default:
              innerText.verticalAlignment = Text.AlignTop;
              break;
          }
      }
  }
  ```

- **Step 4 - 添加属性赋值：**在**parseParameters.js中的parseParas()**函数中添加属性赋值，这些属性可以是QML自带的属性，也可以是后来添加的属性，例如上面的DYText就添加了下面这些属性：

  ```js
  property string text
  property int fontSize
  property color fontColor
  property bool fontBold
  property string textHAlignment
  property string textVAlignment
  ```

  在parseParameters.js中进行属性赋值时，强烈建议在这一步给控件属性进行默认属性赋值，一般采用三元运算符实现，这样当配置文件没有配置对应的属性时，控件会取得默认值。下面是DYText的属性赋值情况：

  ```js
  case "DYText":
      paras2 = {
          "text": ctrlJson.text ? ctrlJson.text : "NoText",
          "width": typeof(ctrlJson.width) === "number" ? ctrlJson.width : 100,
          "height": typeof(ctrlJson.height) === "number" ? ctrlJson.height : 30,
          "color": ctrlJson.color ? ctrlJson.color : colorSpace.fullTransparentColor,
          "fontSize": ctrlJson.fontSize ? ctrlJson.fontSize : 12,
          "fontBold": ctrlJson.fontBold,
          "fontColor": ctrlJson.fontColor ? ctrlJson.fontColor
          : colorSpace.primaryFontColor,
          "textHAlignment": ctrlJson.textHAlignment ? ctrlJson.textHAlignment : "left",
          "textVAlignment": ctrlJson.textVAlignment ? ctrlJson.textVAlignment : "top",
      }
      break;
  ```

- **Step 5 - 添加为DYArea的组件：**将控件作为Component添加到DYArea中，并将其动态生成出来，主要包括以下两步操作：

  - 创建Component：使用Qt.createComponent()添加控件为DYArea的一个Component
  - 创建对象Object：在generateSubCtrls()函数中添加动态创建过程，使用Component.createObject()创建

  不了解QML的Component和Object也没关系，照着下面的代码写就可以了。简单来说，Component类似于类的概念，而createComponent相当于把类引入到当前控件内，然后使用CreateObject创建出一个具体的Object。按照Qt的定义Component是不会出现在界面上的组件，只有通过创建对象才能显示。

  ```js
  // DYArea.qml
  // 创建Component
  property Component dyTextComp: Qt.createComponent("DYText.qml")
  ...
  // 动态创建对象（在函数generateSubCtrls()内）
  case "DYText":
      ctrlObj = dyTextComp.createObject(objParent, paras);
      break;
  ```

objParent为动态生成的DYText对象ctrlObj的父对象，paras为执行属性赋值获取的属性值。Component.createObject()根据这两个参数动态生成组件。

到此为止，我们就完成了一个控制的定制开发，并将其纳入到了DYQML生态系统中。这之后就可以在json配置文件中，在某个DYArea下配置并动态生成该控件了。

## 控件命名规范

在DYQML中，除了一些基础控件（如DObject、DShowerBase、DControllerBase）以大写字母D开头之外，其他控件均已DY开头，后面跟着控件名。DYQML努力使控件名称在整个系统中保持一致，因此控件名称会从文件名一直贯穿到配置文件。因此在定制自己的控件时，保证在上一节中的Step1、Step4、Step5中的名称保持一致，例如**DYText.qml**的文件名与Step4和Step5中的**"DYText"**相同。这样做可以让开发者在配置文件的过程中能够做到明确的对应关系，并且使配置文件具备更强的可读性。





















